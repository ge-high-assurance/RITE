/* Copyright (c) 2022, General Electric Company, Galois, Inc.
 *
 * All Rights Reserved
 *
 * This material is based upon work supported by the Defense Advanced Research
 * Projects Agency (DARPA) under Contract No. FA8750-20-C-0203.
 *
 * Any opinions, findings and conclusions or recommendations expressed in this
 * material are those of the author(s) and do not necessarily reflect the views
 * of the Defense Advanced Research Projects Agency (DARPA).
 */

 //-- Author: Saswata Paul
 //-- An overlay that consists of the fundamental GSN elements and the relationships between them
 //-- This was inspired by Abha et al.'s work for the 2020 DASC paper (https://github.com/ge-high-assurance/assurance-case/blob/master/DeliveryDrone/DeliveryDroneGSN/gsn.sadl)
 
 
 uri "http://sadl.org/GSN-core.sadl" alias gc
  (note "Developed from the 'Goal Structuring Notation Community Standard, Version 2', January 2018, 
available for download from https://scsc.uk/r141B:1").
 
 import "http://arcos.rack/PROV-S".
 
 
//********************* Connecting GSN terminologies to RACK core ontology classes

// Representing the basic terminologies as RACK entities for now. Will evolve as we understand more
 {  
 	Goal
 		(note "a requirements statement; a claim forming part of an argument"), 
 	Strategy 
 		(note "mediates between a goal and its subgoals, explaining basis of inference"), 
 	Solution
 		(note "presents a reference to an evidence item"), 
 	Context 
 		(alias "scope over which claim of goal is made or strategy applies")
 		(note "applies to argument substructure without repetition"), 
 	Assumption 
 		(alias "an unsubstantiated statement")
 		(note "a goal or strategy in the context of an assumption is only applicable when the assumption is true")
 		(note "applies to argument substructure without repetition")
 		(note "complete sentences in the form of a noun-phrase + a verb-phrase"), 
 	Justification
 		(alias "rational for the associated claim of the goal or strategy")
 		(note "applies only to goal or strategy and not to argument substructure")
 		(note "complete sentences in the form of a noun-phrase + a verb-phrase")
 } are types of THING.


 GSN (alias "Goal Structuring Notation") 
 	(note "a graphical argument notation, capturing a hierarchy of claims") is a type of THING,
 	described by rootGoal with a single value of type Goal.
	

 //-------------------------------------------------------------------------------------------------------

 //********************* Internal relationships between GSN elements (should be agnostic of anything else) 	
 	
 supportedBy (note "indicated by solid arrow head")
 	describes {Goal or Strategy} with values of type {Goal or Strategy or Solution}. 
  	

 inContextOf (note "indicated by hollow arrow head", "Kelly's thesis included Solution in the domain")
 	describes {Goal or Strategy} with values of type {Context or Assumption or Justification}.
 	

 //------------------------------------------------------------------------------------------------------

